Array input[4]
Array colors[6]

colors[0] = "red"
colors[1] = "green"
colors[2] = "blue"
colors[3] = "orange"
colors[4] = "yellow"
colors[5] = "purple"

Array answer[4]
Array hints[4]

VOOR index = 0 TOT 3
    answer[index] = colors[Random(6)]

turn = 0
game = true

TOON "welcome to mastermind"
TOON "im going to ask you for 4 colors, the options are, red, green, orange, yellow, blue and purple"
TOON "input them like this"
TOON "blue"
TOON "red"
TOON "etc..."
TOON "make sure you spell the colors correctly and good luck"

TERWIJL game = true
    found = false
    Array isBlack[4]
    Array usedAnswer[4]
    Array usedGuess[4]

    VOOR index = 0 TOT 3
        isBlack[index] = false
        usedAnswer[index] = false
        usedGuess[index] = false
    
    TOON "guess:"
    VRAAG input[0]
    VRAAG input[1]
    VRAAG input[2]
    VRAAG input[3]
    TOON "you're on turn " PLUS (turn+1) PLUS "/10"

    VOOR index = 0 TOT 3
        ALS input[index]=answer[index]
            isBlack[index] = true
            usedAnswer[index] = true
            usedGuess[index] = true
            hints[index] = "Black"

    VOOR guessIndex = 0 TOT 3
        ALS usedGuess[guessIndex]=false
            isWhite = false
            found = false

            VOOR answerIndex = 0 TOT 3
                ALS found=false EN usedAnswer[answerIndex]=false EN input[guessIndex]=answer[answerIndex]
                    usedAnswer[answerIndex] = true
                    usedGuess[guessIndex] = true
                    isWhite = true
                    found = true
            
            ALS isWhite = true
                hints[guessIndex] = "White"
            ANDERS
                hints[guessIndex] = "none"

    VOOR hintIndex = 0 TOT 3
        TOON (hintIndex+1) PLUS ". " PLUS hints[hintIndex]
    
    turn = turn + 1

    hasWon = true
    VOOR index = 0 TOT 3
        ALS isBlack[index] = false
            hasWon = false
    
    ALS hasWon = true
        TOON "you have won"
        game = false
    ANDERS
        ALS turn >= 10
            TOON "you have run out of turns"
            TOON "the answer was:"

            VOOR index = 0 TOT 3
                TOON answer[index]
            game = false